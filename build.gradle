apply plugin: 'java'

apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'nexus'

group 'ru.finam'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.6.1'
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-URL': 'https://github.com/FinamTrade/slf4j-gwt',
                'Build-Date': new java.text.SimpleDateFormat('HH:mm:ss yyyy/MM/dd').format(new Date()),
        )
    }
    
    dependsOn classes
    from project.sourceSets.main.allSource
    includes = ['**/*']
}

idea {
    project {
        languageLevel = '1.6'
    }
}

repositories {
    mavenCentral()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

configurations { gwtCompile }

dependencies {
    gwtCompile 'com.google.gwt:gwt-dev:2.5.1'
    compile 'com.google.gwt:gwt-user:2.5.1'
    compile 'org.slf4j:slf4j-api:1.7.5'
}

sourceSets {
    main.compileClasspath += configurations.gwtCompile
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

modifyPom {
    project {
        name 'slf4j-gwt'
        description 'Simple slf4j api implementation for GWT'
        url 'https://github.com/FinamTrade/slf4j-gwt'
        inceptionYear '2013'

        scm {
            url 'https://github.com/FinamTrade/slf4j-gwt'
            connection 'scm:git:https://github.com/FinamTrade/slf4j-gwt.git'
            developerConnection 'scm:git:git@github.com:FinamTrade/slf4j-gwt.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        issueManagement {
            url 'https://github.com/FinamTrade/slf4j-gwt/issues'
            system 'GitHub Issues'
        }

        developers {
            developer {
                id 'korzhevskiy'
                name 'Andrei Korzhevskii'
                email 'a.korzhevskiy@gmail.com'
                url 'https://github.com/nordligulv'
            }
        }
    }
}
